      SUBROUTINE EXLIND(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,
     1       VKGS,VKGD,VKGI,VFG,VCORG,KDLNC,KNEQ,VRES,VDLE,KEB,KPB)
C=======================================================================
C     pg 420
C=======================================================================
C     TO EXECUTE BLOCK 'LIND'
C     ASSEMBLE AND SOLVE A LINEAR PROBLEM WHEN MATRIX KG IS STORED
C     BLOCKWISE ON DISK
C=======================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/ASSE/NSYM,NKG,NKE,NDLE
      COMMON/RESO/NEQ,NRES,MRES
      COMMON/LIND/NLBL,NBLM,MKG1,MKG2
      COMMON/ES/M,MR,MP,M1,M2,M3
      DIMENSION KLD(*),VDIMP(*),KLOCE(*),VCORE(*),VPRNE(*),VPREE(*),
     1  KNE(*),VKE(*),VFE(*),VKGS(*),VKGD(*),VKGI(*),VFG(*),VCORG(*),
     2  KDLNC(*),KNEQ(*),VRES(*),VDLE(*),KEB(*),KPB(*)
C-----------------------------------------------------------------------
      REWIND M3
C-----   FORM TABLES EB AND PB DEFINING EQUATION BLOCKS
      CALL EQBLOC(KLD,NLBL,NBLM,NEQ,KEB,KPB)
      WRITE(MP,2000) NBLM
2000  FORMAT(15X,'NUMBER OF BLOCKS IN KG (NBLM)=',I5)
      IF(M.LT.2) GO TO 10
      I1=NBLM+1
      WRITE(MP,2010) (KEB(I),I=1,I1)
2010  FORMAT(/' FIRST EQUATION IN EACH BLOCK (EB)'/(5X,20I5))
      WRITE(MP,2020) (KPB(I),I=1,NBLM)
2020  FORMAT(/' FIRST BLOCK CONNECTED TO EACH BLOCK:(PB)'/(5X,20I5))
C         SAVE FG UNMODIFIED FOR PRESCRIBED B.C.
10    WRITE (M3) (VFG(I),I=1,NEQ)
      IF(M.GE.2) WRITE(MP,2030) (VFG(I),I=1,NEQ)
2030  FORMAT(/' GLOBAL LOAD VECTOR UNMODIFIED FOR THE B.C. (FG)'
     1 /(1X,10E12.5))
C-----  ASSEMBLE KG, MODIFY FG FOR B.C. AND SAVE MODIFIED FG
      CALL ASKGD(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,VKGS,
     1  VKGD,VKGI,VFG,VDLE,VRES,KEB)
      WRITE(M3) (VFG(I),I=1,NEQ)
C-----   PRINT FG
      IF(M.GE.2) WRITE(MP,2040) (VFG(I),I=1,NEQ)
2040  FORMAT(/' GLOBAL LOAD VECTOR MODIFIED FOR THE B.C. (FG)'
     1 /(1X,10E12.5))
C
C-----   SOLVE
C
C20adf    CALL SOLD(VKGS,VKGD,VKGI,VFG,KLD,NEQ,MP,1,1,NSYM,ENERG,KEB,KPB)
20    CALL SOLD(VKGS,VKGD,VKGI,VFG,KLD,NEQ,1,1,NSYM,ENERG,KEB,KPB)
      IF(NSYM.NE.1) WRITE(MP,2050) ENERG
2050  FORMAT(15X,'ENERGY (ENERG)=',1E12.5)
C-----   KG PIVOTS AND DETERMINANT
30    CALL PRPVTS(VKGD)
C-----   PRINT OUT THE SOLUTION
      WRITE(MP,2060)
2060  FORMAT(//' SOLUTION'//)
      CALL PRSOL(KDLNC,VCORG,VDIMP,KNEQ,VFG)
C
C-----   EVALUATE AND PRINT GRADIENTS
C
      CALL ASGRAD(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,VKGS,
     1  VKGD,VKGI,VFG,VDLE,VRES)
C
C------  EVALUATE AND PRINT EaUILIBRIUM RESIDUALS AND REACTIONS
C
C------  READ VECTOR FG AND CHANGE ITS SIGN
C
      REWIND M3
      READ(M3) (VRES(I),I=1,NEQ)
      DO 40 I=1,NEQ
40    VRES(I)=-VRES(I)
C------  ASSEMBLE RESIDUALS AND REACTIONS
      CALL ASRESD(1,1,KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,
     1 VKGS,VKGD,VKGI,VFG,VDLE,VRES,VRES(NEQ+1))
C------  OUTPUT
      WRITE(MP,2070)
2070  FORMAT(//' EQUILIBRIUM RESIDUALS AND REACTIONS'//)
      CALL PRSOL(KDLNC,VCORG,VRES(NEQ+1),KNEQ,VRES)
      RETURN
      END