      SUBROUTINE ASRESD(IRESD,IREAC,KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,
     1  KNE,VKE,VFE,VKGS,VKGD,VKGI,VFG,VDLE,VRES,VREAC)
C=======================================================================
C     pg 411
C=======================================================================
C     TO ASSEMBLE INTERNAL RESIDUALS IN VRES (IF IRESD .EQ.1)
C     AND EXTERNAL REACTIONS IN VREAC (IF IREAC.EQ.1)
C=======================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/ELEM/NELT,NNEL,NTPE,NGRE,ME,NIDENT
      COMMON/ASSE/NSYM
      COMMON/RESO/NEQ
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1  ,ICOD
      COMMON/ES/M,MR,MP,M1,M2
      DIMENSION KLD(*),VDIMP(*),KLOCE(*),VCORE(*),VPRNE(*),VPREE(*),
     1  KNE(*),VKE(*),VFE(*),VKGS(*),VKGD(*),VKGI(*),VFG(*),VDLE(*),
     2  VRES(*),VREAC(*)
C-----    REWIND ELEMENT FILE (M2)
      REWIND M2
C-----    LOOP OVER THE ELEMENTS
      DO 60 IE=1,NELT
C-----    READ AN ELEMENT ON FILE M2
      CALL RDELEM(M2,KLOCE,VCORE,VPRNE,VPREE,KNE)
C-----    EVALUATE INTERPOLATION FUNCTION IF REQUIRED
      IF(ITPE.EQ.ITPE1) GO TO 10
      ICOD=2
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----    FIND ELEMENT D.O.F.
10    CALL DLELM(KLOCE,VFG,VDIMP,VDLE)
C-----    EVALUATE ELEMENT REACTIONS
      ICOD=6
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----   PRINT ELEMENT REACTIONS
      IF(M.GE.2) WRITE(MP,2000) IEL,(VFE(I),I=1,IDLE)
2000  FORMAT(/' REACTIONS (FE) , ELEMENT:',I5/(10X,10E12.5))
      IF(IRESD.NE.1) GO TO 20
C-----   ASSEMBLE INTERNAL RESIDUALS
      CALL ASSEL(0,1,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,VRES)
20    IF(IREAC.NE.1) GO TO 60
C-----   ASSEMBLE EXTERNAL REACTIONS
C       MODIFY TERMS IN KLOCE SUCH THAT PRESCRIBED D.O.F. ARE THE ONLY
C       ASSEMBLED ONES
      DO 50 ID=1,IDLE
      IF(KLOCE(ID)) 30,50,40
30    KLOCE(ID)=-KLOCE(ID)
      GO TO 50
40    KLOCE(ID)=0
50    CONTINUE
      CALL ASSEL(0,1,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,VREAC)
60    ITPE1=ITPE
      RETURN
      END