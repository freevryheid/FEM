      SUBROUTINE ASKG(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,
     1  VKGS,VKGD,VKGI,VFG,VDLE,VRES)
C=====================================================================
C     pg 410
C     TO ASSEMBLE GLOBAL MATRIX KG (ELEMENT FUNCTION 3)
C       TAKING INTO ACCOUNT OF NON ZERO PRESCRIBED D.O.F.
C===============================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/COND/NCLT,NCLZ,NCLNZ
      COMMON/ELEM/NELT,NNEL,NTPE,NGRE,ME,NIDENT
      COMMON/ASSE/NSYM
      COMMON/RESO/NEQ
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1 ,ICOD
      COMMON/ES/M,MR,MP,M1,M2
      DIMENSION KLD(*),VDIMP(*),KLOCE(*),VCORE(*),VPRNE(*),VPREE(*),
     1  KNE(*),VKE(*),VFE(*),VKGS(*),VKGD(*),VKGI(*),VFG(*),VDLE(*),
     2  VRES(*),KEB(1)
C----------------------------------------------------------------------
C-----    REWIND ELEMENT FILE (M2)
      REWIND M2
C-----    LOOP OVER THE ELEMENTS
        DO 30 IE=1,NELT
C-----    READ AN ELEMENT ON FILE M2
      CALL RDELEM(M2,KLOCE,VCORE,VPRNE,VPREE,KNE)
C         SKIP COMPUTATION IF IDENTICAL ELEMENTS ENCOUNTERED
      IF(NIDENT.EQ.1.AND.IE.GT.1) GO TO 20
C-----    EVALUATE INTERPOLATION FUNCTIONS IF REQUIRED
      IF(ITPE.EQ.ITPE1) GO TO 10
      ICOD=2
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----    FORM ELEMENT MATRIX
10    ICOD=3
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----    PRINT ELEMENT MATRIX
      IF(M.LT.2) GO TO 20
      IF(NSYM.EQ.0) IKE=IDLE*(IDLE+1)/2
      IF(NSYM.EQ.1) IKE=IDLE*IDLE
      WRITE(MP,2000) IEL,(VKE(I),I=1,IKE)
2000  FORMAT(/' MATRIX (KE) , ELEMENT:',I5/(10X,10E12.5))
C-----    MODIFY FG FOR NON ZERO PRESCRIBED D.O.F.
20    IF(NCLNZ.NE.0) CALL MODFG(IDLE,NSYM,KLOCE,VDIMP,VKE,VFG)
C-----    ASSEMBLE
      CALL ASSEL(1,0,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,VFG)
30    ITPE1=ITPE
      RETURN
      END