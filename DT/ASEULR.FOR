      SUBROUTINE ASEULR(IKT,VCORG,KDLNC,VDIMP,KNEQ,KLD,KLOCE,VCORE,
     1  VPRNE,VPREE,KNE,VKE,VME,VFE,VDLE,VKGS,VKGD,VKGI,VFG,VRES,
     2  VDLG,VDLE0,VDLG0,VFG0)
C=======================================================================
C     pg 433
C=======================================================================
C     TO ASSEMBLE THE RESIDUALS AND THE GLOBAL MATRIX (IF IKT.EQ.1)
C     WHILE LOOPING OVER THE ELEMENTS (FOR EULER METHOD)
C=======================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/ELEM/NELT,NNEL,NTPE,NGRE,ME,NIDENT
      COMMON/ASSE/NSYM
      COMMON/RESO/NEQ
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1  ,ICOD
      COMMON/NLIN/EPSDL,XNORM,OMEGA,XPAS,DPAS,DPAS0,NPAS,IPAS,NITER,
     1  ITER,IMETH
      COMMON/ES/M,MR,MP,M1,M2
      DIMENSION VCORG(*),KDLNC(*),VDIMP(*),KNEQ(*),KLD(*),KLOCE(*),
     1  VCORE(*),VPRNE(*),VPREE(*),KNE(*),VKE(*),VME(*),VFE(*),VDLE(*),
     2  VKGS(*),VKGD(*),VKGI(*),VFG(*),VRES(*),VDLG(*),VDLE0(*),
     3  VDLG0(*),VFG0(*)
      DATA UN/1.D0/
C-----------------------------------------------------------------------
      CC=DPAS*OMEGA
      IFE=0
      IF(ITER.GT.1) IFE=1
C        REWIND ELEMENT FILE (ME)
      REWIND M2
C        LOOP OVER THE ELEMENTS
      DO 90 IE=1,NELT
C        READ AN ELEMENT
      CALL RDELEM(M2,KLOCE,VCORE,VPRNE,VPREE,KNE)
C        EVALUATE INTERPOLATION FUNCTIONS IF REQUIRED
      IF(ITPE.EQ.ITPE1) GO TO 10
      ICOD=2
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C        FIND ELEMENT D.O.F. FROM VFG
10    CALL DLELM(KLOCE,VDLG,VDIMP,VDLE)
C        COMPUTE THE RESIDUAL K.U.
      ICOD=6
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C        COMPUTE MATRIX M
      ICOD=5
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VME,VFE)
C        COMPUTE MATRIX K IF REQUIRED
      IF(IKT.EQ.0) GO TO 15
      ICOD=3
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C        RESIDUALS OF THE FIRST ITERATION IN EACH STEP (LINEAR)
15    IF(ITER.GT.1) GO TO 20
      CALL ASSEL(0,1,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,VFG)
      GO TO 60
C        RESIDUALS AFTER FIRST ITERATION
20    CALL DLELM(KLOCE,VDLG0,VDIMP,VDLE0)
      DO 30 I=1,IDLE
      VDLE(I)=(VDLE0(I)-VDLE(I))/DPAS
30    VFE(I)=-OMEGA*VFE(I)
C        PRODUCT M . U
      VFE(1)=VFE(1)+VME(1)*VDLE(1)
      II=1
      DO 50 J=2,IDLE
      J1=J-1
      DO 40 I=1,J1
      II=II+1
      VFE(I)=VFE(I)+VME(II)*VDLE(J)
40    VFE(J)=VFE(J)+VME(II)*VDLE(I)
      II=II+1
50    VFE(J)=VFE(J)+VME(II)*VDLE(J)
C        MATRIX  M + DPAS.OMEGA. K
60    IF(IKT.EQ.0) GO TO 80
      II=0
      DO 70 I=1,IDLE
      DO 70 J=I,IDLE
      II=II+1
70    VKE(II)=VKE(II)*CC+VME(II)
C        ASSEMBLE THE RESIDUAL AND THE GLOEAL MATRIX
80    CALL ASSEL(IKT,IFE,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,
     1 VRES)
90    ITPE1=ITPE
      RETURN
      END