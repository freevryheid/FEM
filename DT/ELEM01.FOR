      SUBROUTINE ELEM01(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C======================================================================
C     pg 194
C======================================================================
C     QUADRATIC ELEMENT FOR ANISOTROPIC HARMONIC PROBLEMS
C       IN 1,2 OR 3 DIMENSIONS :
C         1 DIMENSION: 3 NODES ELEMENT
C         2 DIMENSIONS: 8 NODES ISOPARAMETRIC ELEMENT
C         3 DIMENSIONS: 20 NODES ISOPARAMETRIC ELEMENT
C     NUMBER OF INTEGRATION POINTS : 2 IN EACH DIRECTION
C     NUMBER OF DEGREES OF FREEDOM PER NODE : 1
C     ELEMENT MATRIX OR VECTOR FORMED BY THIS SUBPROGRAM
C       ACCORDING TO ICODE VALUE :
C          ICODE.EQ.1 RETURN OF PARAMETERS
C          ICODE.EQ.2 EVALUATE INTERPOLATION FUNCTIONS AND
C                      NUMERICAL INTEGRATION COEFFICIENTS
C          ICODE.EQ.3 ELEMENT MATRIX (VKE)
C     ICODE.EQ.4 TANGENT MATRIX (VKE).....NOT WRITTEN
C          ICODE.EQ.S MASS MATRIX (VKE)
C          ICODE.EQ.6 K.U PRODUCT (VFE)
C     ICODE.EQ.7 ELEMENT LOAD (VFE).....NOT WRITTEN
C          ICODE.EQ.8 PRINT GRADIENTS
C     ELEMENT PROPERTIES
C          VPREE(1) COEFFICIENT DX
C          VPREE(2) COEFFICIENT DY
C          VPREE(3) COEFFICIENT DZ
C          VPREE(4) SPECIFIC HEAT CAPACITY C
C=====================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/COOR/NDIM
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1 ,ICODE,IDLE0,INEL0,IPG0
      COMMON/ES/M,MR,MP
      DIMENSION VCORE(*),VPRNE(*),VPREE(*),VDLE(*),VKE(*),VFE(*)
C.........CHARACTERISTIC DIMENSIONS OF THE ELEMENT
C           (VALID UP TO 3 DIMENSIONS)
C     DIMENSION VCPG(IPG),VKPG(NDIM*IPG),XYZ(NDIM)
      DIMENSION VCPG(9),VKPG(27),XYZ(3)
C     DIMENSION VJ (NDIM*NDIM),VJ1(NDIM*NDIM)
      DIMENSION VJ (9),VJ1(9)
C DIMENSION VNIX( INEL*NDIM),VNI ((1+NDIM)*INEL*IPG),IPGKED(NDIM)
      DIMENSION VNIX(60),VNI(2160),IPGKED(3)
C         NUMBER OF G.P. IN KSI,ETA,DZETA DIRECTION
      DATA IPGKED/3,3,3/
C.......
      DATA ZERO/0.D0/ EPS/1.D-6/
      IKE=IDLE*(IDLE+1)/2
C
C-------  CHOOSE FUNCTION T0 BE EXECUTED
C
      GO TO (100,200,300,400,500,600,700,800),ICODE
C
C-------  RETURN ELEMENT PARAMETERS IN COMMON 'RGDT'
C
100   GO TO (110,120,130),NDIM
110   IDLE0=3
      INEL0=3
      IPG0=3
      RETURN
120   IDLE0=8
      INEL0=8
      IPG0=9
      RETURN
130   IDLE0=20
      INEL0=20
      IPG0=27
      RETURN
C
C-------  EVALUATE COORDINATES, WEIGHTS, FUNCTIONS N AND
C-------    THEIR DERIVATIVES AT G.P.
C
200   CALL GAUSS(IPGKED,NDIM,VKPG,VCPG,IPG)
      CALL NI01(VKPG,VNI)
      RETURN
C
C-------  COMPUTE ELEMENT STIFFNESS MATRIX
C
C-------  INITIALIZE VKE
300   DO 310 I=1,IKE
310   VKE(I)=ZERO
C-------  LOOP OVER THE INTEGRATION POINTS
      INI=1+INEL
      DO 330 IG=1,IPG
C-------  EVALUATE THE JACOBIAN MATRIX, ITS INVERSE AND ITS DETERMINANT
      CALL JACOB(VNI(INI),VCORE,NDIM,INEL,VJ,VJ1,DETJ)
      IF(DETJ.LT.EPS) WRITE(MP,2000) IEL,IG,DETJ
2000  FORMAT(' *** ELEM ',I5,' P.G. ',I3,' DET(J)=',E12.5)
C-------   PERFORM DETJ*WEIGHT
      COEF=VCPG(IG)*DETJ
C-------  EVALUATE FUNCTIONS D(NI)/D(X)
      CALL DNIDX(VNI(INI),VJ1,NDIM,INEL,VNIX)
C-------  ACCUMULATE TERMS OF THE ELEMENT MATRIX
      IK=0
      DO 320 J=1,IDLE
      DO 320 I=1,J
      I1=I
      I2=J
      C=ZERO
      DO 315 IJ=1,NDIM
      C=C+VNIX(I1)*VNIX(I2)*VPREE(IJ)
      I1=I1+IDLE
315   I2=I2+IDLE
      IK=IK+1
320   VKE(IK)=VKE(IK)+C*COEF
C-------  NEXT G.P.
330   INI=INI+(NDIM+1)*INEL
      RETURN
C
C-------  EVALUATE ELEMENT TANGENT MATRIX
C
400   CONTINUE
      RETURN

C-------- MASS MATRIX
C
500   DO 510 I=1,IKE
510   VKE(I)=ZERO
      IF(VPREE(4).EQ.ZERO) RETURN
      INI=0
      DO 530 IG=1,IPG
C-------  EVALUATE THE JACOBIAN MATRIX
      I1=INI+INEL+1
      CALL JACOB(VNI(I1),VCORE,NDIM,INEL,VJ,VJ1,DETJ)
C-------  COMPUTE THE WEIGHT
      COEF=VCPG(IG)*DETJ*VPREE(4)
C-------  TERMS OF THE MASS MATRIX
      IK=0
      DO 520 J=1,IDLE
      DO 520 I=1,J
      IK=IK+1
      I1=INI+I
      I2=INI+J
520   VKE(IK)=VKE(IK)+VNI(I1)*VNI(I2)*COEF
530   INI=INI+(NDIM+1)*INEL
      RETURN
C
C-------  EVALUATE THE ELEMENT RESIDUAL
C
600   DO 605 I=1,INEL
605   VFE(I)=ZERO
      INI=1+INEL
      DO 640 IG=1,IPG
C-------  EVALUATE THE JACOBIAN MATRIX AND THE DERIVATIVES OF N IN X,Y,
      CALL JACOB(VNI(INI),VCORE,NDIM,INEL,VJ,VJ1,DETJ)
      CALL DNIDX(VNI(INI),VJ1,NDIM,INEL,VNIX)
C-------   COMPUTE THE COMMON COEFFICIENT
      COEF=VCPG(IG)*DETJ
C-------  VPREE*B*VDLE PRODUCT
      I1=0
      DO 620 I=1,NDIM
      C=ZERO
      DO 610 J=1,INEL
      I1=I1+1
610   C=C+VNIX(I1)*VDLE(J)
620   VJ(I)=C*COEF*VPREE(I)
C------- (BT)*VJ PRODUCT
      DO 630 I=1,INEL
      I1=I-INEL
      DO 630 J=1,NDIM
      I1=I1+INEL
630   VFE(I)=VFE(I)+VNIX(I1)*VJ(J)
640   INI=INI+(NDIM+1)*INEL
      RETURN
C
C-------  EVALUATE FE
C
700   CONTINUE
      RETURN
C
C-------  EVALUATE AND PRINT GRADIENTS AT G.P.
C
800   WRITE(MP,2010) IEL
2010  FORMAT(//' GRADIENTS IN ELEMENT :',I4//)
      IDECL=(NDIM+1)*INEL
      INI0=1
      INI=1+INEL
      DO 830 IG=1,IPG
      CALL JACOB(VNI(INI),VCORE,NDIM,INEL,VJ,VJ1,DETJ)
      CALL DNIDX(VNI(INI),VJ1,NDIM,INEL,VNIX)
C-------  EVALUATE THE COORDINATES OF THE G.P.
      DO 803 I=1,NDIM
803   XYZ(I)=ZERO
      IC=1
      I0=INI0
      DO 807 IN=1,INEL
      C=VNI(I0)
      DO 805 I=1,NDIM
      XYZ(I)=XYZ(I)+C*VCORE(IC)
805   IC=IC+1
807   I0=I0+1
C-------  EVALUATE THE GRADIENT
      I1=0
      DO 820 I=1,NDIM
      C=ZERO
      DO 810 J=1,IDLE
      I1=I1+1
810   C=C+VNIX(I1)*VDLE(J)
820   VJ(I)=C*VPREE(I)
C-------  PRINT THE GRADIENT
      WRITE(MP,2020) IG,(XYZ(I),I=1,NDIM)
2020  FORMAT(5X,'P.G. :',I3,' COORDINATES :',3E12.5)
      WRITE(MP,2025)(VJ(I),I=1,NDIM)
2025  FORMAT(15X,'GRADIENTS   :',3E12.5)
      INI0=INI0+IDECL
830   INI=INI+IDECL
      WRITE(MP,2030)
2030  FORMAT(//)
      RETURN
      END