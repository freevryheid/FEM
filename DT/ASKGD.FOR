      SUBROUTINE ASKGD(KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,KNE,VKE,VFE,
     1  VKGS,VKGD,VKGI,VFG,VDLE,VRES,KEB)
C=======================================================================
C     pg 422
C=======================================================================
C      TO ASSEMBLE GLOBAL MATRIX KG (ELEMENT FUNCTION TYPE 3)
C      TAKING INTO ACCOUNT OF PRESCRIBED NON ZERO D.O.F.
C      VERSION : MATRIX KG STORED BLOCKWISE ON FILE M4
C=======================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/COND/NCLT,NCLZ,NCLNZ
      COMMON/ELEM/NELT,NNEL,NTPE,NGRE,ME,NIDENT
      COMMON/ASSE/NSYM
      COMMON/RESO/NEQ
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1 ,ICOD
      COMMON/LIND/NLBL,NBLM,MKG1,MKG2
      COMMON/ES/M,MR,MP,M1,M2,M3,M4,M5
      DIMENSION KLD(*),VDIMP(*),KLOCE(*),VCORE(*),VPRNE(*),VPREE(*),
     1 KNE(*),VKE(*),VFE(*),VKGS(*),VKGD(*),VKGI(*),VFG(*),VDLE(*),
     1 VRES(*),KEB(*)
      DATA ZERO/0.D0/
C-----------------------------------------------------------------------
C-----   REWIND FILE M4
      REWIND M4
C-----   LOOP OVER THE BLOCKS
      DO 80 IB=1,NBLM
C-----   INITIALIZE THE BLOCK
      DO 10 I=1,NLBL
      IF(NSYM.EQ.1) VKGI(I)=ZERO
10    VKGS(I)=ZERO
      IE1=KEB(IB)
      IE2=KEB(IB+1)-1
C-----   REWIND ELEMENT FILE (M2)
      REWIND M2
C----- LOOP OVER THE ELEMENTS
      DO 70 IE=1,NELT
C----- READ AN ELEMENT
      CALL RDELEM(M2,KLOCE,VCORE,VPRNE,VPREE,KNE)
C-----   CHECK IF BLOCK IS AFFECTED BY THIS ELEMENT
      DO 20 ID=1,IDLE
      J=KLOCE(ID)
      IF(J.LT.IE1.OR.J.GT.IE2) GO TO 20
      GO TO 40
20    CONTINUE
30    IF(IB.NE.1.OR.(NCLNZ.EQ.0.AND.IB.EQ.1)) GO TO 70
C-----    EVALUATE INTERPOLATION FUNCTIONS IF REQUIRED
40    IF(ITPE.EQ.ITPE1) GO TO 50
      ICOD=2
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----   FORM ELEMENT MATRIX
50    ICOD=3
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----   PRINT ELEMENT MATRIX
      IF(M.LT.2) GO TO 60
      IF(NSYM.EQ.0) IKE=IDLE*(IDLE+1)/2
      IF(NSYM.EQ.1) IKE=IDLE*IDLE
      WRITE(MP,2000) IEL,(VKE(I),I=1,IKE)
2000  FORMAT(/' MATRIX (KE) , ELEMENT:',I5/(10X,10E12.5))
C-----   MODIFY FG FOR THE PRESCRIBED NON ZERO D.O.F.
60    IF(NCLNZ.NE.0.AND.IB.EQ.1) CALL MODFG(IDLE,NSYM,KLOCE,VDIMP,VKE,
     1 VFG)
C-----   ASSEMBLE
      CALL ASSELD(1,0,IDLE,NSYM,IE1,IE2,KLOCE,KLD,VKE,VFE,VKGS,VKGD,
     1 VKGI,VFG)
      ITPE1=ITPE
70    CONTINUE
C-----   END OF A BLOCK
      WRITE(M4) (VKGS(I),I=1,NLBL)
      IF(NSYM.EQ.1) WRITE(M4) (VKGI(I),I=1,NLBL)
      IF(M.LT.2) GO TO 80
      WRITE(MP,2010) IB,(VKGS(I),I=1,NLBL)
2010  FORMAT(' UPPER TRIANGLE BLOCK OF (KG) NO:',I5/(1X,10E12.5))
      IF(NSYM.EQ.1) WRITE(MP,2020) IB,(VKGI(I),I=1,NLBL)
2020  FORMAT(' LOWER TRIANGLE BLOCK OF (KG) NO:',I5/(1X,10E12.5))
80    CONTINUE
      IF(M.GE.2) WRITE(MP,2030) (VKGD(I),I=1,NEQ)
2030  FORMAT(' DIAGONAL OF (KG)'/(1X,10E12.5))
      RETURN
      END 