      SUBROUTINE ASNEWT(IKT,KLD,VDIMP,KLOCE,VCORE,VPRNE,VPREE,
     1 KNE,VKE,VFE,VKGS,VKGD,VKGI,VFG,VDLE,VRES)
C=======================================================================
C     pg 428
C=======================================================================
C     TO ASSEMBLE THE RESIDUALS AND THE GLOBAL MATRIX (IF IKT.EQ.1)
C     WHILE LOOPING OVER THE ELEMENTS
C     (FOR THE NEWTON-RAPHSON METHOD)
C==================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      COMMON/ELEM/NELT,NNEL,NTPE,NGRE,ME,NIDENT
      COMMON/ASSE/NSYM
      COMMON/RESO/NEQ
      COMMON/RGDT/IEL,ITPE,ITPE1,IGRE,IDLE,ICE,IPRNE,IPREE,INEL,IDEG,IPG
     1 ,ICOD
      COMMON/ES/M,MR,MP,M1,M2
      DIMENSION KLD(*),VDIMP(*),KLOCE(*),VCORE(*),VPRNE(*),VPREE(*),
     1  KNE(*),VKE(*),VFE(*),VKGS(*),VKGD(*),VKGI(*),VFG(*),VDLE(*),
     2  VRES(*)
C-----------------------------------------------------------------------
C-----  REWIND ELEMENT FILE M2
      REWIND M2
C-----  LOOP OVER THE ELEMENTS
      DO 40 IE=1,NELT
C-----  READ AN ELEMENT
      CALL RDELEM(M2,KLOCE,VCORE,VPRNE,VPREE,KNE)
C-----  EVALUATE INTERPOLATION FUNCTIONS IF REQUIRED
      IF(ITPE.EQ.ITPE1) GO TO 10
      ICOD=2
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----   FIND THE D.O.F. OF THE ELEMENT FROM VFG
10    CALL DLELM(KLOCE,VFG,VDIMP,VDLE)
C-----   CALCULATE ELEMENT RESIDUALS AND CHANGE THEIR SIGN
      ICOD=6
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
      DO 20 I=1,IDLE
20    VFE(I)=-VFE(I)
C-----   EVALUATE GLOBAL MATRIX
      IF(IKT.EQ.0) GO TO 30
      ICOD=4
      CALL ELEMLB(VCORE,VPRNE,VPREE,VDLE,VKE,VFE)
C-----   ASSEMBLE THE RESIDUALS AND THE GLOBAL MATRIX
30    CALL ASSEL(IKT,1,IDLE,NSYM,KLOCE,KLD,VKE,VFE,VKGS,VKGD,VKGI,VRES)
40    ITPE1=ITPE
      RETURN
      END