      SUBROUTINE BLLIND
C=======================================================================
C     pg 419
C=======================================================================
C     TO CALL BLOCK 'LIND'
C     TO ASSEMBLE AND TO SOLVE A LINEAR PROBLEM WHEN MATRIX KG IS
C     STORED BLOCKWISE ON DISK
C=================================================================
      IMPLICIT REAL*8(A-H,O-Z)
      REAL*4 TBL
      COMMON/COOR/NDIM,NNT,NDLN,NDLT
      COMMON/ELEM/NUL(4),ME
      COMMON/ASSE/NSYM,NKG,NKE,NDLE
      COMMON/RESO/NEQ,NRES,MRES
      COMMON/LIND/NLBL,NBLM,MKG1,MKG2
      COMMON/ES/M,MR,MP,M1,M2,M3,M4,M5
      COMMON/ALLOC/NVA,IVA,IVAMAX,NREEL
      COMMON/LOC/LCORG,LDLNC,LNEQ,LDIMP,LPRNG,LPREG,LLD,LLOCE,LCORE,LNE,
     1  LPRNE,LPREE,LDLE,LKE,LFE,LKGS,LKGD,LKGI,LFG,LRES,LDLG
      COMMON VA(1000000)
      DIMENSION TBL(10),IN(3)
      DATA TBL/4HKGS ,4HKGD ,4HKGI ,4HFG  ,4HKE  ,4HFE  ,4HRES ,4HDLE ,
     1  4HEB  ,4HPB  /,DEUX/2.D0/,NBLMAX/100/
C-----------------------------------------------------------------------
C-----  FILE NUMBERS
      IF(M1.EQ.0) M1=MR
      IF(M2.EQ.0) M2=ME
      IF(M3.EQ.0) M3=MRES
      IF(M4.EQ.0) M4=MKG1
      IF(M5.EQ.0) M5=MKG2
C-----  READ BLOCK PARAMETERS
      READ(M1,1000) IN
1000  FORMAT(3I5)
      IF(IN(1).NE.0) NRES=1
      NLBL=IN(2)
      NBLM=IN(3)
      WRITE(MP,2000) M,NRES
2000  FORMAT(//' ON DISK ASSEMBLAGE AND LINEAR SOLUTION (M=',I2,')'/
     1 ' ',42('=')/15X,'INDEX FOR RESIDUAL COMPUTATION     (NRES)=',I5)
      IF(LKGD.EQ.1) CALL ESPACE(NEQ,1,TBL(2),LKGD)
      IF(LFG.EQ.1) CALL ESPACE(NEQ,1,TBL(4),LFG)
      IF(LKE.EQ.1) CALL ESPACE(NKE,1,TBL(5),LKE)
      IF(LFE.EQ.1) CALL ESPACE(NDLE,1,TBL(6),LFE)
      IF(LRES.EQ.1) CALL ESPACE(NDLT,1,TBL(7),LRES)
      IF(LDLE.EQ.1) CALL ESPACE(NDLE,1,TBL(8),LDLE)
C-----   FIND BLOCK LENGTH
      I3=2
      I2=1+NSYM
      IF(NLBL.EQ.0) GO TO 10
      IF(NBLM.EQ.0) NBLM=NKG/NLBL+2
      GO TO 30
10    I1=NVA-IVA-(2*NBLMAX+2)/NREEL-1
      IF(I1.GE.(NKG*I2+2)) GO TO 20
C-----  CASE WHERE MATRIX IS TO BE SEGMENTED
      NLBL=I1/(DEUX*I2)
      NBLM=NKG/NLBL+2
      GO TO 30
C-----  CASE WHERE MATRIX IS IN CORE
20    NLBL=NKG
      NBLM=1
      I3=1
30    WRITE(MP,2010) NLBL,NBLM
2010  FORMAT(
     1 15X,'BLOCKS LENGTH IN KG                           (NLBL)=',I5/
     2 15X,'MAX. NUMBER OF BLOCKS IN KG                         =',I5)
      CALL ESPACE(NBLM+1,0,TBL(9),LEB)
      CALL ESPACE(NBLM,0,TBL(10),LPB)
      IF(LKGS.EQ.1) CALL ESPACE(NLBL*I3,1,TBL(1),LKGS)
      IF(NSYM.EQ.1.AND.LKGI.EQ.1) CALL ESPACE(NLBL*I3,1,TBL(3),LKGI)
      CALL EXLIND(VA(LLD),VA(LDIMP),VA(LLOCE),VA(LCORE),VA(LPRNE),
     1            VA(LPREE),VA(LNE),VA(LKE),VA(LFE),VA(LKGS),VA(LKGD),
     2            VA(LKGI),VA(LFG),VA(LCORG),VA(LDLNC),VA(LNEQ),
     3            VA(LRES),VA(LDLE),VA(LEB),VA(LPB))
      RETURN
      END