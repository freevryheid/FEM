Cadf      SUBROUTINE SOLD(VKGS,VKGD,VKGI,VFG,KLD,NEQ,MP,IFAC,ISOL,NSYM,ENERG
      SUBROUTINE SOLD(VKGS,VKGD,VKGI,VFG,KLD,NEQ,IFAC,ISOL,NSYM,ENERG
     1  ,KEB,KPB)
C=====================================================================
C     pg 288
C=====================================================================
C TO SOLVE A LINEAR SYSTEM (SYMMETRICAL OR NOT).
C     THE MATRIX IS STORED ON FILE M4 BY SKYLINES.
C AFTER TRIANGULARIZATION IT IS STORED ON FILE M5
C       INPUT
C          VKGS,VKGD,VKGI    SYSTEM MATRIX : UPPER, DIAGONAL AND LOWER
C                            PARTS
C          VFG               SECOND MEMBER
C          KLD               ADDRESSES OF COLUMN TOP TERMS
C          NEQ               NUMBER OF EQUATIONS
C          MP                OUTPUT DEVICE NUMBER
C          IFAC              IF IFAC.EQ.1 TRIANGULARIZATION OF
C                            THE MATRIX
C          ISOL              IF ISOL.EQ.1 COMPUTE SOLUTION FROM THE
C                            TRIANGULARIZED MATRIX
C          NSYM              INDEX FOR NON SYMMETRIC PROBLEM
C          KEB               NUMBER OF FIRST EQUATION IN EACH
C                            BLOCK
C          KPB               NUMBER OF FIRST BLOCK CONNECTED TO EACH
C                            BLOCK
C       OUTPUT
C          VKGS,VKGD,VKGI    TRIANGULARIZED MATRIX (IF IFAC.EQ.1
C          VFG               SOLUTION (IF ISOL.EQ.1)
C          ENERG             SYSTEM ENERGY (IF NSYM.EQ.O)
C======================================================================
      IMPLICIT REAL*8 (A-H,O-Z)
      COMMON/LIND/NLBL,NBLM
      COMMON/ES/M,MR,MP,M1,M2,M3,M4,M5
      DIMENSION VKGS(*),VKGD(*),VKGI(*),VFG(*),KLD(*),KEB(*),KPB(*)
      DATA ZERO/0.0D0/
C----------------------------------------------------------------------
      REWIND M4
      REWIND M5
      IK=1
      IF(VKGD(1).NE.ZERO) GO TO 5
      WRITE(MP,2000) IK
      STOP
5     ENERG=ZERO
C
C-------  FOR EACH BLOCK TO BE TRIANGULARIZED
C
      J1MIN=NLBL+1
      J1MAX=NLBL+NLBL
      DO 105 IB=1,NBLM
C-------  READ A BLOCK TO BE TRIANGULARIZED
      READ(M4) (VKGS(I),I=1,NLBL)
      IF(NSYM.EQ.1) READ(M4) (VKGI(I),I=1,NLBL)
C-------  PARAMATERS FOR BLOCK IB
      IK0=KEB(IB)
      IK1=KEB(IB+1)-1
      IB0=KPB(IB)
      J0=KLD(IK0)-1
      IF(IB0.EQ.IB) GO TO 11
C-------  BACKSPACE ON CONNECTED BLOCKS
      I1=IB-IB0
      DO 10 I=1,I1
      BACKSPACE M5
      IF(NSYM.EQ.1) BACKSPACE M5
10    CONTINUE
C-------  FOR EACH CONNECTED BLOCK (INCLUDING BLOCK IB ITSELF)
11    DO 103 IBC=IB0,IB
      IF(IBC.EQ.IB) GO TO 12
      READ(M5) (VKGS(I),I=J1MIN,J1MAX)
      IF(NSYM.EQ.1) READ(M5) (VKGI(I),I=J1MIN,J1MAX)
C-------  PARAMETERS OF CONNECTED BLOCK
12    II0=KEB(IBC)
      II1=KEB(IBC+1)-1
      JC0=KLD(II0)-1
      IF(IBC.NE.IB) JC0=JC0-NLBL
C
C-------  FOR EACH COLUMN OF BLOCK IB TO BE MODIFIED
C
      DO 100 IK=IK0,IK1
      JHK=KLD(IK)-J0
C-----  ADDRESS OF NEXT COLUMN TOP TERM IK+1
      JHK1=KLD(IK+1)-J0
C-----  HEIGHT OF COLUMN IK (INCLUDE UPPER AND DIAGONAL TERMS)
      LHK=JHK1-JHK
      LHK1=LHK-1
C-----  ROW OF FIRST TERM TO BE MODIFIED IN COLUMN IK
      IMIN=IK-LHK1
      IMIN1=IMIN-1
C-------  ROW OF LAST TERM TO BE MODIFIED IN COLUMN IK
      IMAX=IK-1
      IF(LHK1.LT.0) GO TO 100
      IF(IFAC.NE.1) GO TO 90
      IF(NSYM.EQ.0) GO TO 14
      IB1=IB
      IF(IMIN1.LT.IK0) IB1=IB0
      IF(IBC.EQ.IB1) VKGI(JHK)=VKGI(JHK)/VKGD(IMIN1)
14    IF(IBC.EQ.IB.AND.IK.EQ.IK0) GO TO 40
      IF(LHK1.EQ.0) GO TO 40
C-------  FIND FIRST AND LAST ROW OF COLUMN IK AFFECTED
C         BY CONNECTED BLOCK IBC
      IMINC=MAX0(IMIN,II0)
      IMAXC=MIN0(IMAX,II1)
      IF(IMINC.GT.IMAXC) GO TO 40
C
C-----  MODIFY NON DIAGONAL TERMS OF COLUMN IK
C
      JCK=JHK+IMINC-IMIN1
      JHJ=KLD(IMINC)-JC0
C-------  FOR EACH TERM TO BE MODIFIED, LOCATED AT JCK
      DO 30 IJ=IMINC,IMAXC
      JHJ1=KLD(IJ+1)-JC0
C-----  NUMBER OF MODIFICATIVE TERMS OF COEFFICIENT LOCATED AT JCK
      IC=MIN0(JCK-JHK,JHJ1-JHJ)
      IF(IC.LE.0.AND.NSYM.EQ.0) GO TO 20
      C1=ZERO
      IF(IC.LE.0) GO TO 17
      J1=JHJ1-IC
      J2=JCK-IC
      IF(NSYM.EQ.1) GO TO 15
      VKGS(JCK)=VKGS(JCK)-SCAL(VKGS(J1),VKGS(J2),IC)
      GO TO 20
15    VKGS(JCK)=VKGS(JCK)-SCAL(VKGI(J1),VKGS(J2),IC)
      C1=SCAL(VKGS(J1),VKGI(J2),IC)
17    VKGI(JCK)=(VKGI(JCK)-C1)/VKGD(IJ)
20    JCK=JCK+1
30    JHJ=JHJ1
C
C-----  MODIFY DIAGONAL TERM
C
40    IF(IBC.NE.IB) GO TO 90
      JCK=JHK
      CDIAG=ZERO
      DO 70 IJ=IMIN1,IMAX
      C1=VKGS(JCK)
      IF(NSYM.EQ.1) GO TO 50
      C2=C1/VKGD(IJ)
      VKGS(JCK)=C2
      GO TO 60
50    C2=VKGI(JCK)
60    CDIAG=CDIAG+C1*C2
70    JCK=JCK+1
      VKGD(IK)=VKGD(IK)-CDIAG
      IF(VKGD(IK)) 90,80,90
80    WRITE(MP,2000) IK
2000  FORMAT(' *** ERROR, ZERO PIVOT EQUATION ',I5)
      STOP
C
C-----  SOLVE LOWER TRIANGULAR SYSTEM
C
90    IF(ISOL.NE.1) GO TO 100
      IF(IBC.NE.IB) GO TO 100
      IF(NSYM.NE.1) VFG(IK)=VFG(IK)-SCAL(VKGS(JHK),VFG(IMIN1),LHK)
      IF(NSYM.EQ.1) VFG(IK)=VFG(IK)-SCAL(VKGI(JHK),VFG(IMIN1),LHK)
100   CONTINUE
C-------  NEXT CONNECTED BLOCK
103   CONTINUE
C-------  END OF ELIMINATION OF THIS BLOCK
      IF(IB.EQ.NBLM) GO TO 105
      WRITE(M5) (VKGS(I),I=1,NLBL)
      IF(NSYM.EQ.1)  WRITE(M5) (VKGI(I),I=1,NLBL)
105   CONTINUE
      IF(ISOL.NE.1) RETURN
C
C-----  SOLVE DIAGONAL SYSTEM
C
      IF(NSYM.EQ.1) GO TO 120
      DO 110 IK=1,NEQ
      C1=VKGD(IK)
      C2=VFG(IK)/C1
      VFG(IK)=C2
110   ENERG=ENERG+C1*C2*C2
C
C-----  SOLVE UPPER TRIANGULAR SYSTEM
C
120   IB=NBLM
      IK0=KEB(IB)-1
      J0=KLD(IK0+1)-1
      IK=NEQ+1
      JHK1=KLD(IK)-J0
C-------  FOR EVERY EQUATION FROM NEQ TO 1
130   IK=IK-1
C-------  READ A BLOCK IF REQUIRED
      IF(IK.NE.IK0) GO TO 135
      BACKSPACE M5
      IF(NSYM.EQ.1) BACKSPACE M5
      READ(M5) (VKGS(I),I=1,NLBL)
      IF(NSYM.EQ.1) READ(M5) (VKGI(I),I=1,NLBL)
      BACKSPACE M5
      IF(NSYM.EQ.1) BACKSPACE M5
      IB=IB-1
      IK0=KEB(IB)-1
      J0=KLD(IK0+1)-1
      JHK1=KLD(IK+1)-J0
C-------  MODIFY THE UNKNOWN VECTOR
135   IF(NSYM.EQ.1) VFG(IK)=VFG(IK)/VKGD(IK)
      IF(IK.EQ.1) RETURN
      C1=VFG(IK)
      JHK=KLD(IK)-J0
      JBK=JHK1-1
      IF(JHK.GT.JBK) GO TO 150
      IJ=IK-JBK+JHK-1
      DO 140 JCK=JHK,JBK
      VFG(IJ)=VFG(IJ)-VKGS(JCK)*C1
140   IJ=IJ+1
150   JHK1=JHK
      GO TO 130
      END